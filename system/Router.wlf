// system/Router.wlf

gerombolan Router
    garap init()
        $this.routes = []
    
    garap get($path, $handler, $middleware)
        $this.add_route("GET", $path, $handler, $middleware)
        
    garap post($path, $handler, $middleware)
        $this.add_route("POST", $path, $handler, $middleware)
        
    garap add_route($method, $path, $handler, $middleware)
        $pattern = "^" + string_replace($path, "{", "([^/]+)")
        $pattern = string_replace($pattern, "}", "") + "$"
        
        menowo $middleware == kopong
            $middleware = []

        $route = {
            "method": $method,
            "path": $path,
            "pattern": $pattern,
            "handler": $handler,
            "middleware": $middleware
        }
        plumbungan($this.routes, $route)
        
    garap handle($request)
        $method = $request["metode"]
        $path = $request["path"]
        
        // --- SMART PUBLIC FOLDER ---
        // Jika file ada di folder public/, kirimkan langsung
        $public_path = "public" + $path
        menowo string_contains($path, ".")
            $content = moco_file($public_path)
            menowo $content != "ERROR: gagal moco"
                balekno $content
        
        $i = 0
        $len = dowo($this.routes)
        
        track ($i < $len)
            $route = $this.routes[$i]
            
            menowo $route["method"] == $method
                $pattern = $route["pattern"]
                
                menowo string_regex_match($path, $pattern)
                    $handler = $route["handler"]
                    
                    $params = string_regex_capture($path, $pattern)
                    $request["params"] = $params

                    $middleware_list = $route["middleware"]
                    $mw_count = dowo($middleware_list)
                    $j = 0
                    $mw_failed = palsu
                    $mw_response = nil

                    track ($j < $mw_count)
                        $middleware = $middleware_list[$j]
                        $mw_res = $middleware.handle($request)
                        
                        menowo $mw_res["error"] == bener
                            $mw_failed = bener
                            $mw_response = $mw_res
                            $j = $mw_count
                        
                        $j = $j + 1
                    
                    menowo $mw_failed
                        balekno http_error($mw_response["code"], $mw_response["message"])
                    
                    balekno $handler($request)
                
            $i = $i + 1
        
        balekno http_error(404, "Halaman ora ketemu")
