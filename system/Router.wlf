// system/Router.wlf

gerombolan Router
    garap init()
        $this.routes = []
    
    garap get($path, $handler)
        $this.add_route("GET", $path, $handler)
        
    garap post($path, $handler)
        $this.add_route("POST", $path, $handler)
        
    garap add_route($method, $path, $handler)
        // Convert {param} to regex group ([^/]+)
        $pattern = "^" + string_replace($path, "{", "([^/]+)")
        $pattern = string_replace($pattern, "}", "") + "$"
        
        $route = {
            "method": $method,
            "path": $path,
            "pattern": $pattern,
            "handler": $handler
        }
        plumbungan($this.routes, $route)
        
    garap handle($request)
        $method = $request["metode"]
        $path = $request["path"]
        
        // Static file serving legacy (manual for now)
        menowo $path == "/logo.png"
            balekno moco_file("public/logo.png")
        menowo $path == "/logo.ico"
            balekno moco_file("public/logo.ico")
        
        $i = 0
        $len = dowo($this.routes)
        
        track ($i < $len)
            $route = $this.routes[$i]
            
            menowo $route["method"] == $method
                $pattern = $route["pattern"]
                
                menowo string_regex_match($path, $pattern)
                    $handler = $route["handler"]
                    
                    // Extract parameters
                    $params = string_regex_capture($path, $pattern)
                    $request["params"] = $params
                    
                    balekno $handler($request)
                
            $i = $i + 1
        
        // Default 404 response
        menowo string_contains($path, "/api/")
            balekno http_error(404, "Route ora ketemu")
        yenora
            balekno "<!DOCTYPE html><html><head><title>404 Not Found</title></head><body><h1>404 Not Found</h1><p>The page you are looking for could not be found.</p></body></html>"
